query
  ::= entityName project? select? group? order? restrict?

project
  ::= "{" "*" ("-" attributeName)* attributeProject* "}"
    | "{" attributeProject* "}"

attributeProject
  ::= label? identifier "(" (attributeExpression | "*") ")"
    | label applyExpression
    | label qualifiedAttributeExpression
    | label "(" expression ")"
    | label? query
    | label? attributeExpression
    | label? "&" attributeName

label
  ::= identifier ":"

entityName
  ::= identifier

attributeName
  ::= identifier

applyExpression
  ::= identifier "(" expression ("," expression)* ")"

attributeExpression
  ::= identifier

qualifiedAttributeExpression
  ::= attributeName ("." attributeName)*

select
  ::= "[" booleanExpression "]"

group
  ::= "/" expression ("," expression)* "/"

order
  ::= "<" expression ("ASC" | "DESC")? ("NULLS" ("FIRST" | "LAST"))? ("," expression ("ASC" | "DESC")? ("NULLS" ("FIRST" | "LAST"))?)* ">"

restrict
  ::= "|" integer ("," integer)? "|"
    | "|" "," integer "|"

booleanExpression
  ::= orExpression

orExpression
  ::= orExpression "OR" andExpression
    | andExpression

andExpression
  ::= andExpression "AND" notExpression
    | notExpression

notExpression
  ::= "NOT" booleanPrimary
    | booleanPrimary

booleanPrimary
  ::= expression ("<=" | ">=" | "<" | ">" | "=" | "!=" | "LIKE" | "ILIKE" | ("NOT" "LIKE") | ("NOT" "ILIKE")) expression
    | expression (("NOT" "BETWEEN") | "BETWEEN") expression "AND" expression
    | expression "IS" "NOT"? "NULL"
    | expression "NOT"? "IN" ("(" (expression ("," expression)* | query) ")")
    | "EXISTS" "(" query ")"
    | "TRUE" | "FALSE" | "NULL"
    | qualifiedAttributeExpression
    | "(" booleanExpression ")"

expression
  ::= additive

additive
  ::= additive ("+" | "-") multiplicative
    | multiplicative

multiplicative
  ::= multiplicative ("*" | "/") primary
    | primary

primary
  ::= float | integer | string | "TRUE" | "FALSE" | "NULL" | "*"
    | "CASE" ("WHEN" booleanExpression "THEN" expression)+ ("ELSE" expression)? "END"
    | applyExpression
    | qualifiedAttributeExpression
    | "&" attributeName ("." attributeName)*
    | "-" primary
    | "(" query ")"
    | "(" expression ")"

identifier ::= letter | "_" | "$" (letter | "_" | "$" | digit)*

/* color: #009688 (teal) */
