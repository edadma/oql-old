query
  ::= entityName project? select? group? order? restrict?

project
  ::= "{" "*" ("-" attributeName)* attributeProject* "}"
    | "{" attributeProject* "}"

attributeProject
  ::= label? identifier "(" argument ")"
    | label applyExpression
    | label qualifiedAttributeExpression
    | label "(" expression ")"
    | label? query
    | label? attributeExpression
    | label? "&" attributeName

label
  ::= identifier ":"

argument
  ::= attributeExpression | "*"

entityName
  ::= identifier

attributeName
  ::= identifier

applyExpression
  ::= identifier "(" expressions ")"

attributeExpression
  ::= identifier

qualifiedAttributeExpression
  ::= identifiers

identifiers
  ::= attributeName ("." attributeName)*

select
  ::= "[" booleanExpression "]"

group
  ::= "/" expressions "/"

order
  ::= "<" ordering ("," ordering)* ">"

ordering
  ::= expression ("ASC" | "DESC")? ("NULLS" ("FIRST" | "LAST"))?

restrict
  ::= "|" integer ("," integer)? "|"
    | "|" "," integer "|"

expressions
  ::= expression ("," expression)*

booleanExpression
  ::= orExpression

orExpression
  ::= orExpression "OR" andExpression
    | andExpression

andExpression
  ::= andExpression "AND" notExpression
    | notExpression

notExpression
  ::= "NOT" booleanPrimary
    | booleanPrimary

booleanPrimary
  ::= expression comparison expression
    | expression (("NOT" "BETWEEN") | "BETWEEN") expression "AND" expression
    | expression "IS" "NOT"? "NULL"
    | expression "NOT"? "IN" ("(" (expressions | query) ")")
    | "EXISTS" "(" query ")"
    | booleanLiteral
    | qualifiedAttributeExpression
    | "(" booleanExpression ")"

comparison
  ::= "<=" | ">=" | "<" | ">" | "=" | "!=" | "LIKE" | "ILIKE" | ("NOT" "LIKE") | ("NOT" "ILIKE")

booleanLiteral
  ::= "TRUE" | "FALSE"

expression
  ::= additive

additive
  ::= additive ("+" | "-") multiplicative
    | multiplicative

multiplicative
  ::= multiplicative ("*" | "/") primary
    | primary

literalExpression
  ::= float | integer | string | booleanLiteral

primary
  ::= literalExpression | "*" | caseExpression | applyExpression | qualifiedAttributeExpression
    | "&" identifiers
    | "-" primary
    | "(" query ")"
    | "(" expression ")"

caseExpression
  ::= "CASE" ("WHEN" booleanExpression "THEN" expression)+ ("ELSE" expression)? "END"

identifier ::= letter | "_" | "$" (letter | "_" | "$" | digit)*

/* color: #009688 (teal) */
